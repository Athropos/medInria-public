on run
	do shell script ("rm -f @CMAKE_INSTALL_PREFIX@/TmpPackage.dmg @CMAKE_INSTALL_PREFIX@/medInria-@medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@.dmg")
	do shell script ("hdiutil create -srcfolder \"@CMAKE_INSTALL_PREFIX@/bin/medInria.app\" -volname \"medInria @medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@\" -fs HFS+ -fsargs \"-c c=64,a=16,e=16\" -format UDRW -size 400M @CMAKE_INSTALL_PREFIX@/TmpPackage.dmg")
	set device to do shell script ("hdiutil attach -readwrite -noverify -noautoopen \"@CMAKE_INSTALL_PREFIX@/TmpPackage.dmg\" | egrep '^/dev/' | sed 1q | awk '{print $1}' ")
	
	do shell script ("mkdir -p /Volumes/\"medInria @medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@\"/.background")
	do shell script ("cp -p @PROJECT_SOURCE_DIR@/app/medInria/pixmaps/medInria-logo-dmg.png /Volumes/\"medInria @medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@\"/.background")
	
	set dmgIcon to (POSIX file "@PROJECT_SOURCE_DIR@/app/medInria/pixmaps/medInria_dmg_mountedimage.icns") as alias
	set dmgFolder to (POSIX file "/Volumes/medInria @medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@") as alias
	
	tell application "Finder"
		activate
		set infoWindow to open information window of dmgIcon
		set infoWindowName to name of infoWindow
	end tell
	
	tell application "System Events"
		tell application process "Finder"
			tell window infoWindowName
				keystroke tab
				delay 5
				keystroke "c" using command down
			end tell
		end tell
	end tell
	
	tell application "Finder"
		close infoWindow
		set infoWindow to open information window of dmgFolder
		set infoWindowName to name of infoWindow
	end tell
	
	tell application "System Events"
		tell application process "Finder"
			tell window infoWindowName
				keystroke tab
				delay 5
				keystroke "v" using command down
			end tell
		end tell
	end tell
	
	tell application "Finder"
		close infoWindow
	end tell
	
	tell application "Finder"
		tell disk "medInria @medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@"
			open
			set current view of container window to icon view
			set toolbar visible of container window to false
			set statusbar visible of container window to false
			set bounds of container window to {400, 100, 960, 390}
			set theViewOptions to the icon view options of container window
			set arrangement of theViewOptions to not arranged
			set icon size of theViewOptions to 72
			set background picture of theViewOptions to file ".background:medInria-logo-dmg.png"
			make new alias file at container window to POSIX file "/Applications" with properties {name:"Applications"}
			set position of item "medInria" of container window to {100, 200}
			set position of item "Applications" of container window to {460, 200}
			close
			open
			update without registering applications
			delay 5
		end tell
	end tell
	
	do shell script ("sync")
	do shell script ("sync")
	do shell script "hdiutil detach " & device
	do shell script ("hdiutil convert \"@CMAKE_INSTALL_PREFIX@/TmpPackage.dmg\" -format UDZO -imagekey zlib-level=9 -o \"@CMAKE_INSTALL_PREFIX@/medInria-@medInria_VERSION_MAJOR@.@medInria_VERSION_MINOR@.@medInria_VERSION_BUILD@.dmg\"")
	do shell script ("rm -f @CMAKE_INSTALL_PREFIX@/TmpPackage.dmg")
end run
